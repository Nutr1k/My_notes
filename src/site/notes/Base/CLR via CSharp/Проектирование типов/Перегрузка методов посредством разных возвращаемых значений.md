---
{"dg-publish":true,"permalink":"/base/clr-via-c-sharp/proektirovanie-tipov/peregruzka-metodov-posredstvom-raznyh-vozvrashhaemyh-znachenij/"}
---


В C# перегрузка методов возможно только посредством реализации методов с разным числом парамеров и различными типами этих параметров. Однако некоторые языки поддерживают перегрузку посредством возвращаемого типа. Такая возможность отсутствует в C#, но IL её поддерживает.

Пример вызова перегруженных операторов отличающихся лишь возвращаемыми значениями:

```csharp
internal class Program
{
	public static string Get()
	{
		Console.WriteLine("string Get");
		return "5";
	}

	public static int Get()
	{
		Console.WriteLine("int Get");
		return 5;
	}

	public static void Get()
	{
		Console.WriteLine("void Get");
	}

	public static void Main()
	{
		Program.Get();
		Program.Get();
		Program.Get();
	}
}
```

IL код:
```csharp
// =============== CLASS MEMBERS DECLARATION ===================

.class private auto ansi beforefieldinit Calc.Program
       extends [mscorlib]System.Object
{
  .method public hidebysig static string 
          Get() cil managed
  {
    // Размер кода:       16 (0x10)
    .maxstack  8
    IL_0000:  ldstr      "string Get"
    IL_0005:  call       void [mscorlib]System.Console::WriteLine(string)
    IL_000a:  ldstr      "5"
    IL_000f:  ret
  } // end of method Program::Get

  .method public hidebysig static int32  Get() cil managed
  {
    // Размер кода:       12 (0xc)
    .maxstack  8
    IL_0000:  ldstr      "int Get"
    IL_0005:  call       void [mscorlib]System.Console::WriteLine(string)
    IL_000a:  ldc.i4.5
    IL_000b:  ret
  } // end of method Program::Get

  .method public hidebysig static void  Get() cil managed
  {
    // Размер кода:       11 (0xb)
    .maxstack  8
    IL_0000:  ldstr      "void Get"
    IL_0005:  call       void [mscorlib]System.Console::WriteLine(string)
    IL_000a:  ret
  } // end of method Program::Get

  .method public hidebysig static void  Main() cil managed
  {
    .entrypoint
    // Размер кода:       18 (0x12)
    .maxstack  8
    IL_0000:  call       string Calc.Program::Get()
    IL_0005:  pop
    IL_0006:  call       int32 Calc.Program::Get()
    IL_000b:  pop
    IL_000c:  call       void Calc.Program::Get()
    IL_0011:  ret
  } // end of method Program::Main

  .method public hidebysig specialname rtspecialname 
          instance void  .ctor() cil managed
  {
    // Размер кода:       7 (0x7)
    .maxstack  8
    IL_0000:  ldarg.0
    IL_0001:  call       instance void [mscorlib]System.Object::.ctor()
    IL_0006:  ret
  } // end of method Program::.ctor

} // end of class Calc.Program
```

Видно, что IL код может вызывать перегрузку методы не просто по имени и перегружаемым параметрам, а по типу возвращаемого значения:
```csharp
    IL_0000:  call       string Calc.Program::Get()
    IL_0005:  pop
    IL_0006:  call       int32 Calc.Program::Get()
    IL_000b:  pop
    IL_000c:  call       void Calc.Program::Get()
```

![Pasted image 20230310183333.png](/img/user/Files/Image/Pasted%20image%2020230310183333.png)

![Pasted image 20230311145032.png](/img/user/Files/Image/Pasted%20image%2020230311145032.png)

Файл полностью:
[[overload.il]]
[[overload.exe]]